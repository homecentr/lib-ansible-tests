plugins{
    id 'com.jfrog.bintray' version '1.8.5'
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id 'maven'
}

group = 'io.homecentr'

sourceCompatibility = 1.8

jar {
    baseName = 'ansible-tests'
}

repositories{
    mavenCentral()
    jcenter()
    maven {
        url  "https://dl.bintray.com/homecentr/maven"
    }
}

dependencies{
    implementation "org.testcontainers:testcontainers:1.14.3"
    implementation 'io.homecentr:testcontainers-extensions:1.4.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.11.2'

    testImplementation group: 'junit', name: 'junit', version: '4.13'

    testCompile "org.mockito:mockito-core:3.4.6"
}

task sourcesJar(type: Jar, dependsOn: classes){
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc){
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts{
    archives sourcesJar
    archives javadocJar
}

bintray{
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_API_KEY")
    publications = ['AnsibleTests']
    publish = true
    
    pkg{
        repo = String.valueOf(System.getenv('BINTRAY_REPO')).toLowerCase()
        userOrg = String.valueOf(System.getenv('BINTRAY_ORG')).toLowerCase()
        name = 'ansible-tests'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/homecentr/lib-ansible-tests.git'
        websiteUrl = 'https://github.com/homecentr/lib-ansible-tests'
        issueTrackerUrl = 'https://github.com/homecentr/lib-ansible-tests/issues'
        publicDownloadNumbers = true

        githubRepo = 'homecentr/lib-ansible-tests'
        githubReleaseNotesFile = 'README.md'

        version {
            name = String.valueOf(System.getenv("RELEASE_VERSION")).replace("v", "")
            vcsTag = String.valueOf(System.getenv("GITHUB_SHA")).toLowerCase()
            released = new Date()
        }
    }
}

publishing {
    publications {
        AnsibleTests(MavenPublication){
            from components.java
            groupId group
            artifactId 'ansible-tests'
            version String.valueOf(System.getenv("RELEASE_VERSION")).replace("v", "")
            artifact javadocJar
            artifact sourcesJar
        }
    }
}

test {
    afterTest { desc, result ->
        logger.quiet "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
    }
}